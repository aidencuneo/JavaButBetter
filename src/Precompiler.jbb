public static class Precompiler

// These fields persist for all files
List<RegexRule> regexRules = new ArrayList<>()

ArrayList<string> precompileFile(string className, string code):
    lines = Tokeniser.splitFile(code)

    for i in len(lines)
        tok = Tokeniser.tokLine(lines[i], true)
        next if !tok
        tok.remove(0) // Ignore INDENT token

        keep = precompileStatement(tok)
        lines.set(i, "") if !keep

    return lines

bool precompileStatement(ArrayList<Token> tok):
    return false if !tok

    types = Tokeniser.getTokenTypes(tok)
    startTok = types[0]
    endTok = types[-1]
    f = -1
    f2 = -1

    // Custom regex
    if startTok == Token.Type.REGEX
        if len(tok) < 2
            // Error!
            return false

        stage = "pre"

        // If there is an identifier after the first token, take this as the regex insertion stage
        if tok[1].type == Token.Type.ID
            stage = tok[1].value

        // Find the expression
        if (f = Compiler.findTokenType(tok, Token.Type.EXPR)) == -1
            // Error!
            return false

        pair = tok[f].value[1:-1]
        pairTok = Tokeniser.tokLine(pair)
        println pairTok

        // Not enough tokens in the expression?
        if len(pairTok) < 2
            // Error!
            return false

        find = pairTok[0].value[1:-1]
        replace = pairTok[1].value[1:-1]

        regexRules.add(RegexRule(find, replace, stage))
        return false

    // Leave line unchanged (keep)
    return true

string applyRegexRules(string code, int attempts):
    for rule in regexRules
        for i in [..attempts]
            break if !rule.find(code)
            code = rule.apply(code)
    return code

string applyRegexRules(string code):
    return applyRegexRules(code, 10000)
